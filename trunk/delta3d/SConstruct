import os 
import re
import sys
import time
import smtplib
import SCons.Util

from email.MIMEText import MIMEText

# TODO:
# install headers, libs, to prefix
# make clean
# fix dtCore/dtUtil auto-install
# fix conf messages
# doxygen
# make packaged release
# CVS support
# move DLLs to bin
# use debug RTI libs?

########################
# Build error emailing #
########################

# set this to true to enable build emailing
email = 'false'

# fill this in with interested parties
peopleToEmail = []

# replcae with your local outgoing SMTP server
smtpServer = 'mysmtp@server.com'

# this is who the build email will come from
fromAddress = 'myaddress@company.com'

# Function to calculate current time for logging
def current_time() :
   now = time.localtime()
   display = time.strftime('%A, %B %d %Y, %X', now)
   return display

# Function to email the result of the build
def email_results() :
	file = open('BuildLog.txt', 'r')
	buffer = MIMEText(file.read())
	buffer['Subject'] = 'Editor Build Log'
	file.close()

	s = smtplib.SMTP()
	s.connect(smtpServer) 
	s.sendmail(fromAddress, peopleToEmail, buffer.as_string())
	s.close()

###########
# Options #
###########

# Open the error log
errorLog = open('BuildLog.txt', 'w')

print 'Build started: ' + current_time() + ''
errorLog.write('Build started: ' + current_time() + '\n\n')

# detemine the OS
if sys.platform == 'win32' :
   OS = 'windows'	
elif sys.platform == 'linux2' or sys.platform == 'linux-i386' :
   OS = 'linux'
else :
   print 'Build Failed: Unsupported platform'
   errorLog.write('Build Failed: Unsupported platform: ' + sys.platform)
   errorLog.close()
   env.Exit(-1) 

rtiSLibs = [   'bitvector', 
               'if', 
               'libfedtime13', 
               'librti13', 
               'mcast', 
               'mempool', 
               'mersenne',
               'mpiif', 
               'msgstats', 
               'netutil',
               'parser', 
               'rid',
               'rtis', 
               'timers', 
               'urlreader', 
               'vterm', 
               'zlib' ] 
   
# some basic command-line options
optCache = 'options.cache'
opts = Options(optCache)
opts.AddOptions(
   EnumOption( 'mode', 'Build as either debug or release', 'release',
               allowed_values = ( 'debug', 'release' ),
               map = {}, ignorecase = 1 ),
   BoolOption( 'no_warnings', 'Disable all warnings', 0 ),
   PathOption( 'prefix', 'Directory to install under', os.getcwd() ),
   PackageOption( 'boost', 'Boost installation directory', 'no' ),
   PackageOption( 'rti', 'RTI installation directory', 'no' ),
   ( 'rti_libs', 'List of RTI libraries to link against', rtiSLibs )
)
      
env = Environment( options = opts )

# append the outside env to ours
for K in os.environ.keys():
   if K in env['ENV'].keys() and K in [ 'PATH', 'LD_LIBRARY_PATH', 'LIB', 'INCLUDE' ]:
      env['ENV'][K]=SCons.Util.AppendPath( env['ENV'][K], os.environ[K] )
   else:
      env['ENV'][K]=os.environ[K]
      
env.Append( CPPPATH = [os.path.join( os.getcwd(), 'inc' ) ] ) # Delta3D includes
env.Append( CPPPATH = [os.path.join( os.getcwd(), 'ext', 'inc' ) ] ) # Dependencies includes
env.Append( CPPPATH = [os.path.join( sys.prefix, 'include' ) ] ) # Python includes

env.Append( LIBPATH = [os.path.join( os.getcwd(), 'lib' ) ] ) # Delta3D libs
env.Append( LIBPATH = [os.path.join( os.getcwd(), 'ext', 'lib' ) ] ) # Dependencies libs
env.Append( LIBPATH = [os.path.join( sys.prefix, 'libs' ) ] ) # Python libs

def findFile( findThis, startDirs ) :
   for startDir in startDirs :
      for root, dirs, files in os.walk( startDir ) :
         if findThis in files :
            return os.path.join( root, findThis )
   return None
   
# Boost Python
if env.get('boost') not in [ 0, 1 ] and os.path.exists( env.get('boost') ) and os.path.isdir( env.get('boost') ) :

   env.Append( CPPPATH = [ env.get('boost') ] )
   env.Append( LIBPATH = [ os.path.join( env.get('boost'), 'libs', 'python', 'build', 'bin-stage' ) ] )   
      
 # RTI 
if env.get('rti') not in [ 0, 1 ] and os.path.exists( env.get('rti') ) and os.path.isdir( env.get('rti') ) :

   rtiHeader = findFile( 'RTI.hh', [ env.get('rti') ] )
   
   if OS == 'windows' :
      pattern = 'rti.*?\.dll'
   else :
      pattern = 'librti.*?\.so'
   
   rtiLib = None
   for root, dirs, files in os.walk( env.get('rti') ) :
      for file in files :
         if re.search( pattern, file ) is not None :
            rtiLib = os.path.join( root, file )
            break;
      if rtiLib is not None :
         break;
    
   env.Append( CPPPATH = [ os.path.dirname( rtiHeader ) ] )
   env.Append( LIBPATH = [ os.path.dirname( rtiLib ) ] )
   
srcLibPaths = [ 'dtABC', 'dtAudio', 'dtChar','dtCore', 'dtHLA', 'dtScript', 'dtUtil', 'dtVis', 'gui_fl', 'python', 'soarx' ]
env.Append(LIBPATH=[ os.path.join( os.getcwd(), 'src', elem ) for elem in srcLibPaths ])

# TODO: can we remove these?
if OS == 'linux' :
   env.Append(CPPPATH=['/usr/X11R6/include','/usr/include','/usr/local/include'])
   env.Append(LIBPATH=['/usr/X11R6/lib','/usr/lib','/usr/local/lib'])

Help( opts.GenerateHelpText( env ) )
opts.Save( 'delta3d.conf', env )

if env.get('mode') == 'debug':
   mode = 'debug'
elif env.get('mode') == 'release':
   mode = 'release'
else:
   print 'Build Failed: Unsupported mode'
   errorLog.write('Build Failed: Unsupportedmode: ' + sys.platform)
   errorLog.close()
   env.Exit(-1) 
   
print 'OS: ' + OS
errorLog.write('OS: ' + OS + '\n\n')

# set compiler options for debug/release modes
if mode == 'debug':

   print 'Build Configuration: Debug'
   errorLog.write('Build Configuration: Debug\n\n')
   
   if OS == 'windows':
      env.Append( CPPDEFINES = ['WIN32', '_DEBUG', '_NOAUTOLIBMSG'],
                  CXXFLAGS = ['/EHsc', '/GR', '/MDd' ], #synchronous exception handling (c-?), run-time type info, multi-threaded debug dll
                  LINKFLAGS = ['/NODEFAULTLIB:LIBCMTD', '/NODEFAULTLIB:LIBCD'] ) 
   elif OS == 'linux':
      env.Append( CXXFLAGS=['-g', '-O0', '-pipe'], #deubg info, no optimizations, pipe object data
                  CXXDEFINES=['DEBUG', '_DEBUG'] )
                  
else:

   print 'Build Configuration: Release'
   errorLog.write('Build Configuration: Release\n\n')
   
   if OS == 'windows':
      env.Append( CPPDEFINES = ['WIN32', 'NDEBUG', '_NOAUTOLIBMSG'],
                  CXXFLAGS = ['/EHsc', '/GR', '/MD' ], #synchronous exception handling (c-?), run-time type info, multi-threaded dll
                  LINKFLAGS = ['/NODEFAULTLIB:LIBCMT', '/NODEFAULTLIB:LIBC'] )  
   elif OS == 'linux':
      env.Append( CXXFLAGS=['-O2', '-pipe'], #optimizations, pipe object data
                  CPPDEFINES=['NDEBUG'],
                  LINKFLAGS=['-s'] )
    
if env.get('no_warnings'):
   if OS == 'windows':
      env.Append(CXXFLAGS=['/w'])
   elif OS == 'linux':
      env.Append(CXXFLAGS=['-w'])
else:
   if OS == 'windows':
      env.Append(CXXFLAGS=['/W3'])
   elif OS == 'linux':
      if env['CC'] == 'gcc' or env['CXX'] == 'g++':
         env.Append(CXXFLAGS=['-Wall'])
      if env['CXX'] == 'g++':
            env.Append(CXXFLAGS=['-Wno-non-virtual-dtor'])

#############################################
# Configure: Testing for dependencies vX.X.X
#
# osg0.9.8-2, etc.
# ...
##############################################

# do version, config checking
   
if OS == 'windows' :
   if mode == 'debug' :
      extLibs = { 
         'boost_python' : 'boost_python_debug',
         'osg'          : 'osgd',
         'osgDB'        : 'osgDBd',
         'osgUtil'      : 'osgUtild',
         'osgGL2'       : 'osgGL2d',
         'osgText'      : 'osgTextd',
         'osgSim'       : 'osgSimd',
         'osgFX'        : 'osgFXd',
         'osgParticle'  : 'osgParticled',
         'osgGA'        : 'osgGAd',
         'osgProducer'  : 'osgProducerd',
         'osgTerrain'   : 'osgTerraind',
         'Producer'     : 'Producerd',
         'OpenThreads'  : 'OpenThreadsWin32d',
         'python'       : 'python23',
         'cal3d'        : 'cal3d_d',
         'fltk'         : 'fltkd',
         'gdal'         : 'gdal_i',
         'glgui'        : 'glGUI_d',
         'isense'       : 'isensed',
         'openal'       : 'OpenAL32', 
         'alut'         : 'ALut',         
         'ode'          : 'oded', 
         'sg'           : 'sg_d', 
         'ul'           : 'ul_d', 
         'js'           : 'js_d',  
         'rvrutils'     : 'rvrutilsd',
         'rcfgscript'   : 'rcfgscriptd', 
         'rbody'        : 'ReplicantBodyd',
         'tinyxml'      : 'tinyxmld',
         'User32'       : 'User32',
         'Advapi32'     : 'Advapi32',
         'Rpcrt4'       : 'Rpcrt4',
         'Winmm'        : 'Winmm',
         'Gdi32'        : 'Gdi32',
         'opengl'       : 'Opengl32',
         'winsock'      : 'ws2_32',
         'shell32'      : 'shell32',
         'ole32'        : 'ole32' 
      }
   else :
      extLibs = { 
         'boost_python' : 'boost_python',
         'osg'          : 'osg',
         'osgDB'        : 'osgDB',
         'osgUtil'      : 'osgUtil',
         'osgGL2'       : 'osgGL2',
         'osgText'      : 'osgText',
         'osgSim'       : 'osgSim',
         'osgFX'        : 'osgFX',
         'osgParticle'  : 'osgParticle',
         'osgGA'        : 'osgGA',
         'osgProducer'  : 'osgProducer',
         'osgTerrain'   : 'osgTerrain',
         'Producer'     : 'Producer',
         'OpenThreads'  : 'OpenThreadsWin32',
         'python'       : 'python23',
         'cal3d'        : 'cal3d',
         'fltk'         : 'fltk',
         'gdal'         : 'gdal_i',
         'glgui'        : 'glGUI',
         'isense'       : 'isense',
         'openal'       : 'OpenAL32', 
         'alut'         : 'ALut',         
         'ode'          : 'ode', 
         'sg'           : 'sg', 
         'ul'           : 'ul', 
         'js'           : 'js',  
         'rvrutils'     : 'rvrutils',
         'rcfscript'    : 'rcfgscript', 
         'rbody'        : 'ReplicantBody',
         'tinyxml'      : 'tinyxml',
         'User32'       : 'User32',
         'Advapi32'     : 'Advapi32',
         'Rpcrt4'       : 'Rpcrt4',
         'Winmm'        : 'Winmm',
         'Gdi32'        : 'Gdi32',
         'opengl'       : 'Opengl32',
         'winsock'      : 'ws2_32',
         'shell32'      : 'shell32',
         'ole32'        : 'ole32' 
      }
   
elif OS == 'linux' :
      extLibs = { 
         'boost_python' : 'boost_python',
         'osg'          : 'osg',
         'osgDB'        : 'osgDB',
         'osgUtil'      : 'osgUtil',
         'osgGL2'       : 'osgGL2',
         'osgText'      : 'osgText',
         'osgSim'       : 'osgSim',
         'osgFX'        : 'osgFX',
         'osgParticle'  : 'osgParticle',
         'osgGA'        : 'osgGA',
         'osgProducer'  : 'osgProducer',
         'osgTerrain'   : 'osgTerrain',
         'Producer'     : 'Producer',
         'python'       : 'python2.3',
         'OpenThreads'  : 'OpenThreads',
         'cal3d'        : 'cal3d',
         'fltk'         : 'fltk',
         'gdal'         : 'gdal',
         'glgui'        : 'glgui',
         'isense'       : 'isense',
         'openal'       : 'openal', 
         'ode'          : 'ode', 
         'sg'           : 'plibsg', 
         'ul'           : 'plibul', 
         'js'           : 'plibjs',  
         'rvrutils'     : 'rvrutils',
         'rcfscript'    : 'rcfgscript', 
         'rbody'        : 'rbody',
         'tinyxml'      : 'tinyxml' ,
         'Xxf86vm'      : 'Xxf86vm',
         'uuid'         : 'uuid',
         'opengl'       : 'GL'
      }
else :
   extLibs = {}

conf = Configure(env)

# TODO: don't run for install
if not env.GetOption('clean') and OS == 'linux' and 0:
     
   foundReqLibs = 1
   for lib in extLibs.keys() :
      foundReqLibs = foundReqLibs and conf.CheckLib( extLibs[lib], language = 'C++')         
   foundReqLibs = foundReqLibs and conf.CheckHeader('sigslot.h',language='C++')
   
   env = conf.Finish()

   if foundReqLibs == 0 :   
      print 'Build Failed: Missing required libraries'
      errorLog.write('Build Failed: Missing required libraries\n\n')
      errorLog.close()

      if email == 'true' :
         email_results()
         
      env.Exit(-1)
   
# Documentation generation system
#doxygenBuilder = Builder(action = 'doxygen $SOURCE')
#env.Append(BUILDERS = { 'DoxygenDoc' : doxygenBuilder })
   
#################
# Build Delta3D #
#################

Export( 'env', 'OS', 'mode', 'extLibs' , 'rtiSLibs' )

env.Alias( 'python', [ os.path.join(os.getcwd(), 'src', 'python'),
                       os.path.join(os.getcwd(), 'examples', 'testPython')] )

env.Alias( 'hla', [ os.path.join(os.getcwd(), 'src', 'dtHLA'),
                     os.path.join(os.getcwd(), 'examples', 'testHLA'),
                     os.path.join(os.getcwd(), 'utilities', 'hlaStealthViewer') ] )
                     
SConscript( dirs=['src'] )
SConscript( dirs=['examples'] )
SConscript( dirs=['utilities'] )

#if libs == '[]' :
#   errorLog.write('Failed to create the libraries or executable file')
#   errorLog.write('Build Failed: Build ended: ' + current_time() + '\n')
#   errorLog.close()
#   env.Exit(-1)

# Close the error log and print the results
print 'Build Succeeded: Build ended: ' + current_time()
errorLog.write('Build Succeeded: Build ended: ' + current_time() + '\n')
errorLog.close()

# email the results
if email == 'true' :
   email_results() 
